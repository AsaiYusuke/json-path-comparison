start
  = "$" operators:operators { return operators; }

operators
  = operators:operator* { return operators.reduce((os, o) => os.concat(o), []); }

operator
  = dotNotation
  / union
  / recursiveDescentWithChildren

dotNotation
  = "." children:identifier { return children; }

// TODO Unicode
identifier
  = chars:[a-zA-Z0-9_\-]+ { return [['children', [['index', chars.join("")]]]]; }
  / "*" { return [['children', [['all']]]]; }

union
  = "[" whitespace children:bracketElements whitespace "]" { return [['children', children]]; }

bracketElements
  = x:bracketElement whitespace "," whitespace xs:bracketElements { return [x].concat(xs); }
  / x:bracketElement { return [x]; }

bracketElement
  = "'" element:singleQuotedString "'" { return ['index', element]; }
  / '"' element:doubleQuotedString '"' { return ['index', element]; }
  / "*" { return ['all']; }
  / start:number? ":" end:number? ":" step:nonZeroNumber? { return ['slice', start, end, step]; }
  / start:number? ":" end:number? { return ['slice', start, end, null]; }
  / element:number { return ['index', element]; }
  / "?(" filterExpression:filterExpression ")" { return ['filter', filterExpression]; }

singleQuotedString
  = x:"\\'" xs:singleQuotedString { return x.replace('\\', '') + xs; }
  / x:[^'] xs:singleQuotedString { return x + xs; }
  / ''

doubleQuotedString
  = x:'\\"' xs:doubleQuotedString { return x.replace('\\', '') + xs; }
  / x:[^"] xs:doubleQuotedString { return x + xs; }
  / ''

number
  = "-"? absoluteNumber { return text(); }

nonZeroNumber
  = "-"? [1-9] absoluteNumber? { return text(); }

absoluteNumber
  = [0-9]+ { return text(); }

filterExpression
  = partialA:filterPartial whitespace "==" whitespace partialB:filterPartial { return ["equals", partialA, partialB]; }
  / partial:filterPartial { return ["hasValue", partial]; }

filterPartial
  = "@" operators:operators { return operators; }
  / value:jsonValue { return [["value", value]]; }

// TODO need full JSON spec here + exemption for single quoted strings
jsonValue
  = "null" { return null; }
  / "true" { return true; }
  / "false" { return false; }
  / number:number { return parseInt(number); }
  / "'" string:singleQuotedString "'" { return string; }
  / '"' string:doubleQuotedString '"' { return string; }

whitespace
  = " "*

recursiveDescentWithChildren
  = ".." children:identifier { return [['recursiveDescent']].concat(children); }
  / ".." children:union { return [['recursiveDescent']].concat(children); }
