start
  = "$" operators:operators* recursiveDescent:recursiveDescentWithoutChildren { return operators.reduce((os, o) => os.concat(o), []).concat(recursiveDescent); }
  / "$" operators:operators* { return operators.reduce((os, o) => os.concat(o), []); }

operators
  = children:children { return [['children', children]]; }
  / recursiveDescentWithChildren

children
  = dotNotation
  / union

dotNotation
  = "." path:identifier { return [path]; }

// TODO Unicode
identifier
  = chars:[a-zA-Z0-9_\-]+ { return ['index', chars.join("")]; }
  / "*" { return ['all']; }

union
  = "[" children:bracketElements "]" { return children; }

bracketElements
  = x:bracketElement "," xs:bracketElements { return [x].concat(xs); }
  / x:bracketElement { return [x]; }

bracketElement
  = "'" element:singleQuotedString "'" { return ['index', element]; }
  / '"' element:doubleQuotedString '"' { return ['index', element]; }
  / "*" { return ['all']; }
  / element:number { return ['index', element]; }

singleQuotedString
  = x:"\\'" xs:singleQuotedString { return x + xs; }
  / x:[^'] xs:singleQuotedString { return x + xs; }
  / ''

doubleQuotedString
  = x:'\\"' xs:doubleQuotedString { return x + xs; }
  / x:[^"] xs:doubleQuotedString { return x + xs; }
  / ''

number
  = sign:"-"? digits:[0-9]+ { return sign !== null ? sign + digits.join("") : digits.join(""); }

recursiveDescentWithChildren
  = ".." path:identifier { return [['recursiveDescent'], ['children', [path]]]; }
  / ".." children:union { return [['recursiveDescent'], ['children', children]]; }

recursiveDescentWithoutChildren
  = ".." { return [['recursiveDescent']]; }
